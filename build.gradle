plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

group = 'astronomicalsystem'
version = '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.graphics' ]
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

application {
    mainClass = 'astronomicalsystem.Launcher'
}

tasks.register('customFatJar', Jar) {
    group = "build"
    description = "Crea un Fat JAR con tutte le dipendenze"

    archiveFileName = 'SolarSystem3D-all.jar'

    manifest {
        attributes(
                'Main-Class': 'astronomicalsystem.Launcher'
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}